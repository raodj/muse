# This is a Epidemic Description Language (EDL) description for the
# Ebola model proposed by: Rivers CM, Lofgren ET, Marathe M, Eubank S,
# Lewis BL. Modeling the Impact of Interventions on an Epidemic of
# Ebola in Sierra Leone and Liberia. PLOS Currents Outbreaks. 2014

epidemic EbolaLiberia {

     # The following constants were elicited by Rivers et. al by
     # calibrating the observed Ebola outbreak to a 6 compartment
     # model.
    constants {
        Bi   = 0.160;    # Contact Rate, Community 
        Bh   = 0.062;    # Contact Rate, Hospital
        Bf   = 0.489;    # Contact Rate, Funeral
        A    = 0.08333;  # Incubation rate (period = 12 days)
        Yh   = 0.3086;   # Time until hopitalization 3.24 days
        Ydh  = 0.0993;   # Time from Hospitalization to death (10.07 days)
        Yf   = 0.4975;   # Duration of Traditional Funeral (2.01 days)
        Yi   = 0.0667;   # Duration of infection (15 days)
        Yd   = 0.0751;   # Time from infection to death (13.31 days)
        Yih  = 0.06297;  # Time from Hoptialization to Recovery (15.88 days)
        O1   = 0.5;      # Case Fatality rate, Unhosptalized
        O2   = 0.5;      # Case Fatality rate, Hospitalized
        l    = 0.197;    # Probability a case is hospitalized
    }

    parameters {
        # Currently this model does not use any simulation-time
        # updated parameters.
    }

    compartments {
        s,   # susceptible (can get Ebola)
        e,   # exposed     (incubating virus)
        i,   # infective   (actively spreading infection to others)
        h,   # hospitalized
        f,   # funeral     (infection spread at funerals due to social norms)
        r;   # Recovered
    }

    # The SSA transitions. They are automatically converted to ODE
    # versions by the EDL code-generator.
    transitions {
       # N (total population) is automagically generated.
       s += -1, e += 1 @ ((Bi * s * i) + (Bh * s * h) + (Bf * s * f)) / N;
       e += -1, i += 1 @ (A * e);
       i += -1, h += 1 @ (Yh * l * i);
       i += -1, f += 1 @ (Yd * (1 - l) * O1 * i);
       i += -1, r += 1 @ (Yi * (1 - l) * (1 - O1) * i);
       h += -1, f += 1 @ (Ydh * O2 * h);
       h += -1, r += 1 @ (Yih * (1 - O2) * h);
       f += -1, r += 1 @ (Yf * f);
    }
}

# End of EDL
